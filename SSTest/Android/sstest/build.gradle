apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.southernstars.sstest"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }
        task buildJniHeaders(type: Exec, description: 'Build JNI Headers') {
            def sdkDir = android.sdkDirectory
            def localClasspathDir =  file('src/main/java').absolutePath

            commandLine "javah",
                    '-d', file('src/main/jni').absolutePath,
                    '-classpath', "$sdkDir/platforms/android-29/android.jar:" + localClasspathDir,
                    'com.southernstars.sscore.JSSAngle',
                    'com.southernstars.sscore.JSSDate',
                    'com.southernstars.sscore.JSSDegMinSec',
                    'com.southernstars.sscore.JSSHourMinSec',
                    'com.southernstars.sscore.JSSJPLDEphemeris',
                    'com.southernstars.sscore.JSSMatrix',
                    'com.southernstars.sscore.JSSObjectArray',
                    'com.southernstars.sscore.JSSSpherical',
                    'com.southernstars.sscore.JSSTime',
                    'com.southernstars.sscore.JSSVector'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

task copyAssets ( type: Copy ) {
    description = 'Copies selected files from the common SSData folder into assets/SSData'
    from ( '../../../SSData' ) {
        include 'SolarSystem/Planets.csv'
        include 'SolarSystem/Moons.csv'
        include 'SolarSystem/Asteroids.txt'
        include 'SolarSystem/Comets.txt'
        include 'SolarSystem/DE438/1950_2050.438'
        include 'SolarSystem/Satellites/visual.txt'
        include 'Stars/Brightest.csv'
        include 'Stars/Nearest.csv'
        include 'DeepSky/Messier.csv'
        include 'DeepSky/Caldwell.csv'
        include 'Constellations/Constellations.csv'
        include 'Constellations/Boundaries.csv'
        include 'Constellations/Shapes.csv'
    }
    into 'src/main/assets/SSData'
}

task deleteAssets ( type: Delete ) {
    delete 'src/main/assets/SSData'
}

preBuild.dependsOn copyAssets
clean.dependsOn deleteAssets

